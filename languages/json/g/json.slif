# # ## ### ##### ######## ############# #####################
## JSON specification -- SLIF grammar for use by TclMarpa.
## (c) 2017-2018 Andreas Kupries
#
# References:
# - http://json.org/				Base spec
# - https://tools.ietf.org/html/rfc7159		IETF spec
# - http://seriot.ch/parsing_json.php		Test suite
# - https://core.tcl.tk/akupries/marpa/		Tcl+Marpa
#

:default ::=
	action => [name,values]

lexeme default =
	action => [start,length,value]
       	latm   => 1

:discard ~ whitespace

:start ::= value

value ::= object
      |	  array
      |	  string
      |	  number
      |	  true
      |	  false
      |	  null

object ::= (lbrace) pairs (rbrace)
pairs  ::= pair*
	separator => comma
	proper    => 1
pair   ::= string (colon) value

array    ::= (lbracket) elements (rbracket)
elements ::= element *
	separator => comma
	proper    => 1
element  ::= value

string	 ::= (quote) lstring (quote)
number	 ::= lnumber
true	 ::= ltrue
false	 ::= lfalse
null	 ::= lnull

ltrue	 ~ 'true'
lfalse	 ~ 'false'
lnull	 ~ 'null'

lstring ~ char *

lnumber  ~ int fraction exponent
	 | int          exponent
	 | int fraction
	 | int

int 	 ~ whole
	 | '-' whole

whole    ~ '0'
	 | positive

positive ~ [1-9] digitz
digitz   ~ decimal *
fraction ~ '.' digits
exponent ~ e digits

e	 ~ [eE]
   	 | [eE] '-'
   	 | [eE] '+'

digits   ~ decimal +
decimal  ~ [0-9]

lbrace   ~ '{'
rbrace   ~ '}'
lbracket ~ '['
rbracket ~ ']'
comma    ~ ','
colon    ~ ':'
quote    ~ '"'
char     ~ plain
	 | '\\b' [bfnrt"/\b]
	 | '\\u' hex hex hex hex
plain    ~ [^"\\[:control:]]
hex      ~ [[:xdigit:]]

whitespace ~ [[:space:]] +
