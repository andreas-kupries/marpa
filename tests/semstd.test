# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2017-2018 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## marpa::semstd

kt check Tcl     8.5
kt check tcltest 2

kt require support debug
kt require support debug::caller
kt require support oo::util
kt require support TclOO

kt local   support marpa::c
kt local   testing marpa::runtime::tcl

# # ## ### ##### ######## ############# #####################\

kt source support/common.tcl
kt source support/tcl-core.tcl

# # ## ### ##### ######## ############# #####################
## - nop
## - K
## - locmerge
## - builtin

# # ## ### ##### ######## ############# #####################
## nop

test marpa-semstd-nop-1.0 {nop} -body {
    marpa semstd nop
} -result {}

test marpa-semstd-nop-1.1 {nop} -body {
    marpa semstd nop foo bar snafu
} -result {}

# # ## ### ##### ######## ############# #####################
## K

test marpa-semstd-k-1.0 {K, wrong args, not enough} -body {
    marpa::semstd::K
} -returnCodes error -result [ARGS {wrong # args: should be "marpa::semstd::K x ..."}]

test marpa-semstd-k-2.0 {K} -body {
    marpa::semstd::K x
} -result x

test marpa-semstd-k-2.1 {K} -body {
    marpa::semstd::K x foo snafu bar
} -result x

# # ## ### ##### ######## ############# #####################
## locmerge

test marpa-semstd-locmerge-1.0 {locmerge, wrong args, not enough} -body {
    marpa semstd locmerge
} -returnCodes error -result [ARGS {wrong # args: should be "marpa semstd locmerge id ..."}]

# Taken from location.test, merge2

test marpa-semdstd-locmerge-2.0 {locmerge, non-adjacent} -body {
    marpa semstd locmerge ID [marpa location atom 0 A] [marpa location atom 2 B]
} -returnCodes error -result {Bad merge, non-adjacent locations}

test marpa-semdstd-locmerge-2.1 {locmerge, adjacent, out of proper order} -body {
    marpa semstd locmerge ID [marpa location atom 1 B] [marpa location atom 0 A]
} -returnCodes error -result {Bad merge, non-adjacent locations}

test marpa-semdstd-locmerge-3.0 {locmerge, null, both} -body {
    marpa semstd locmerge ID [marpa location null] [marpa location null]
} -result {{} {} {}}

test marpa-semdstd-locmerge-3.1 {locmerge, null, first only} -body {
    marpa semstd locmerge ID [marpa location null] [marpa location atom 3 C]
} -result {3 3 C}

test marpa-semdstd-locmerge-3.2 {locmerge, null, second only} -body {
    marpa semstd locmerge ID [marpa location atom 3 C] [marpa location null]
} -result {3 3 C}

test marpa-semdstd-locmerge-3.3 {merge2} -body {
    marpa semstd locmerge ID [marpa location atom 0 A] [marpa location atom 1 B]
} -result {0 1 AB}


# # ## ### ##### ######## ############# #####################
## builtin

test marpa-semstd-builtin-1.0 {builtin, wrong args, not enough} -body {
    marpa semstd builtin
} -returnCodes error \
    -result [ARGS {wrong # args: should be "marpa semstd builtin parts id ..."}]

test marpa-semstd-builtin-1.1 {builtin, wrong args, not enough} -body {
    marpa semstd builtin PARTS
} -returnCodes error \
    -result [ARGS {wrong # args: should be "marpa semstd builtin parts id ..."}]

test marpa-semstd-builtin-1.2 {builtin, bad part} -body {
    marpa semstd builtin bogus ID {0 1 AB}
} -returnCodes error -result {Unsupported part "bogus", expected one of g1start, g1length, values, start, end, value, length, rule, name, symbol, ord, or lhs}

# args = locations

# Possible part keys, and their details
##
# - g1start  INT
# - g1length INT
# - start    INT
# - end      INT
# - value    n/a
# - values   n/a
# - length   INT
# - rule     ID
# - name     NAME
# - symbol   NAME
# - lhs      ID
# - ord      INT

testcases k {part expected} {
    g1start    {}
    g1length   1
    start      0
    end        1
    length     2
    value      AB
    values     {{0 1 AB}}
    rule       ID
    {name N}   N
    {symbol N} N
    {lhs L}    L
    {ord 3}    3
} {
    test marpa-semstd-builtin-2.${k}-$part "builtin, part: $part" -body {
	marpa semstd builtin [list $part] ID {0 1 AB}
    } -result $expected
}

# # ## ### ##### ######## ############# #####################
cleanupTests
