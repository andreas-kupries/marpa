#!/usr/bin/env tclsh
# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
##
# (c) 2017-present Andreas Kupries http://wiki.tcl.tk/andreas%20kupries
#                                  http://core.tcl.tk/akupries/
#
# # ## ### ##### ######## ############# #####################
## Regenerate the SLIF boot parser
## Regenerate the LITERAL parser

package require Tcl 8.5
package require marpa::slif::parser
package require marpa::slif::container
package require marpa::slif::semantics
package require marpa::gen
package require marpa::gen::format::cparse-critcl
package require marpa::gen::format::tparse
package require fileutil

#debug on util/benchmarking

if {[llength $argv] > 1} {
    puts "Usage: $argv0 ?/path/to/main/destination/directory?"
    puts "Results are written to:"
    puts " - DST/slif-parser/parser.tcl"
    puts " - DST/slif-literal/lparser.tcl"
    puts ""
    exit 1
}

set ak {Andreas Kupries}
set ja {Jeffrey Kegler + Andreas Kupries}


# Default: Current working directory.
if {![llength $argv]} { lappend argv [pwd] }

set selfdir  [file dirname [file normalize [info script]]]
set meta     [file join $selfdir marpa-tcl-slif slif]
set literals [file join $selfdir marpa-tcl-slif literals]
set dst      [lindex $argv 0]
set dstl     [file join $dst languages]

set topdir   [file dirname $selfdir]
set lang     [file join $topdir languages]
set mindt    [file join $lang min-dt   g min-dt.slif]
set mindtsf  [file join $lang min-dt   g sf.slif]
set heredoc  [file join $lang heredoc  g heredoc.slif]
set json     [file join $lang json     g json.slif]
set dt       [file join $lang doctools g doctools.slif]
set dtsf     [file join $lang doctools g sf.slif]


proc F {n} { format "%.2f" $n }

proc Make {authors name src dst {format cparse-critcl}} {
    file mkdir [file dirname $dst]

    # _ __ ___ _____ ________ _____________ _____________________
    # Process the grammar
    set sz [file size $src]
    puts "Processing $src ($sz bytes) ..."
    marpa::slif::container create GC
    marpa::slif::semantics create SEMA GC
    marpa::slif::parser    create BOOT

    set start  [clock milliseconds]
    set ast    [BOOT process-file $src]
    set done   [clock milliseconds]
    set millis [expr {$done-$start}]
    set sec    [expr {double($millis)/1000}]
    set speed  [expr {$sz/$sec}]
    puts "Processing done in [F $sec] seconds (@ [F $speed] bytes/sec)"

    SEMA process $ast
    # GC now holds the grammar
    BOOT destroy
    # Note: SEMA auto-destroys itself at the end of 'process'.

    # _ __ ___ _____ ________ _____________ _____________________
    # Generate the SLIF parser class
    puts "Generating ..."
    marpa::gen config! version  0
    marpa::gen config! writer   $authors
    marpa::gen config! year     [clock format [clock seconds] -format %Y]
    marpa::gen config! name     $name
    marpa::gen config! operator $::tcl_platform(user)@[info hostname]
    marpa::gen config! tool     [file tail [info script]]
    set parser [marpa::gen::format::$format container GC]
    GC destroy

    # _ __ ___ _____ ________ _____________ _____________________
    # Write to file for debugging.
    puts "Saving to $dst ..."
    fileutil::writeFile $dst $parser

    puts {... OK}
    return
}

# _ __ ___ _____ ________ _____________ _____________________
# Process the slif grammars (meta, and literals)

Make $ja marpa::slif::parser          $meta     $dst/slif-parser/parser.tcl
Make $ak marpa::slif::literal::parser $literals $dst/slif-literal/lparser.tcl

# _ __ ___ _____ ________ _____________ _____________________
# Process the example min-dt grammars, all variants

set dst $dstl/min-dt

Make $ak mindt::parser::c       $mindt   $dst/mindt-lang-c.tcl
Make $ak mindt::parser::tcl     $mindt   $dst/mindt-lang-tcl.tcl tparse

Make $ak mindt::parser::sf::c   $mindtsf $dst/mindt-sf-c.tcl
Make $ak mindt::parser::sf::tcl $mindtsf $dst/mindt-sf-tcl.tcl tparse

# _ __ ___ _____ ________ _____________ _____________________
# Process the example heredoc grammars, all variants

set dst $dstl/heredoc

Make $ak heredoc::parser::c   $heredoc $dst/heredoc-c.tcl
Make $ak heredoc::parser::tcl $heredoc $dst/heredoc-tcl.tcl tparse

# _ __ ___ _____ ________ _____________ _____________________
# Process the example json grammars, all variants

set dst $dstl/json

Make $ak json::parser::c   $json $dst/json-c.tcl
Make $ak json::parser::tcl $json $dst/json-tcl.tcl tparse

# _ __ ___ _____ ________ _____________ _____________________
# Process the example doctools grammars, all variants

set dst $dstl/doctools

Make $ak doctools::parser::c       $dt   $dst/doctools-lang-c.tcl
Make $ak doctools::parser::tcl     $dt   $dst/doctools-lang-tcl.tcl tparse

Make $ak doctools::parser::sf::c   $dtsf $dst/doctools-sf-c.tcl
Make $ak doctools::parser::sf::tcl $dtsf $dst/doctools-sf-tcl.tcl tparse

exit
