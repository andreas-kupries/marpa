# # ## ### ##### ######## ############# #####################
## Copyright 2017 Andreas Kupries
#
# This file is part of MarpaTcl.  MarpaTcl is free software: you can
# redistribute it and/or modify it under the terms of the BSD 3-Clause
# License (Same license as Tcl itself is under).
#
# MarpaTcl is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.

# # ## ### ##### ######## ############# #####################
##
# This file contains a SLIF grammar derived from Jeffrey Kegler's
# original to account for Tcl's idiosyncrasies. And my own ;)
#
# - Removed blessings. They are a thoroughly Perl construct which
#   simply does not make sense for Tcl.
#
# - Encoded the adverb / context relationship into the syntax instead
#   of letting the semantics sort it out.
#
# - Redefined the string and character class syntax to match Tcl with
#   regard to the allowed escape sequences. The important point here
#   is that I encode the relevant constraints into the syntax instead
#   of letting the semantics break on bad input.
#
#   This is actually quite complex because a naive definition of the
#   escape sequences will introduce ambiguities into the grammar. As
#   the characters in a string or class are independent these will
#   multiply, for an exponential blowup with a power between 2 and 4,
#   depending on the mix of escapes.
#
#   As an example, look at the octal escape '\377'. With a naive
#   definition we get 3 possible readings, namely '\3'+'77',
#   '\37'+'7', and '\377'. In a similar manner the unicode escapes
#   ('\u...') will can have up to 4 readings.
#
#  To avoid the problem the grammar is more complex, with the rules
#  for the shorter escape sequences looking at their Follow() context
#  to decide whether to keep or abort them. The basic idea is to treat
#  the definition as a regexp, a finite automaton, which is translated
#  into a set of (indirect) right-recursive rules where the right-most
#  symbol of each rule encodes the state of the automaton for the
#  regexp. As a side note, here Marpa's use of Joop Leo's fix comes
#  into its own, preventing non-linear blowup in the earley items for
#  such rules.

# - Formatting (nice indentations and such)

# # ## ### ##### ######## ############# #####################
## Copyright statement of the original:
#
# Copyright 2015 Jeffrey Kegler
# This file is part of Marpa::R2.  Marpa::R2 is free software: you can
# redistribute it and/or modify it under the terms of the GNU Lesser
# General Public License as published by the Free Software Foundation,
# either version 3 of the License, or (at your option) any later version.
#
# Marpa::R2 is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser
# General Public License along with Marpa::R2.  If not, see
# http://www.gnu.org/licenses/.

# # ## ### ##### ######## ############# #####################
## Let us begin

:default ::=
  action => [name,values]

lexeme default =
  action => [start,length,value]
  latm   => 1

:start ::= statements

statements ::= statement+	name => 'statements/0'

statement ::=
    <start rule>			name => 'statement/0'
  | <empty rule>			name => 'statement/1'
  | <null statement>			name => 'statement/2'
  | <statement group>			name => 'statement/3'
  | <priority rule>			name => 'statement/4'
  | <quantified rule>			name => 'statement/5'
  | <discard rule>			name => 'statement/6'
  | <default rule>			name => 'statement/7'
  | <lexeme default statement>		name => 'statement/8'
  | <discard default statement>		name => 'statement/9'
  | <lexeme rule>    			name => 'statement/10'
  | <completion event declaration>	name => 'statement/11'
  | <nulled event declaration>		name => 'statement/12'
  | <prediction event declaration>	name => 'statement/13'
  | <current lexer statement>		name => 'statement/14'
  | <inaccessible statement>		name => 'statement/15'

<null statement>  		::= ';'								name => 'null statement/0'
<statement group>		::= ('{') statements '}'					name => 'statement group/0'
<default rule>			::= (':default' <op declare bnf>) <adverb list default>		name => 'default rule/0'
<lexeme default statement>	::= ('lexeme' 'default' '=') <adverb list lexeme default>	name => 'lexeme default statement/0'
<discard default statement>	::= ('discard' 'default' '=') <adverb list discard default>	name => 'discard default statement/0'

<start rule> ::=
   (':start' <op declare bnf>) symbol	name => 'start rule/0'
 | ('start' 'symbol' 'is')     symbol	name => 'start rule/1'

<empty rule> ::=
   lhs (<op declare bnf>)   <adverb list bnf empty>	name => 'empty rule/0'
 | lhs (<op declare match>) <adverb list match empty>	name => 'empty rule/1'

<quantified rule> ::=
   lhs (<op declare bnf>)   <single symbol> quantifier <adverb list bnf quantified>	name => 'quantified rule/0'
 | lhs (<op declare match>) <single symbol> quantifier <adverb list match quantified>	name => 'quantified rule/1'

<discard rule> ::=
   (':discard' <op declare match>) symbol            <adverb list discard>	name => 'discard rule/0'
 | (':discard' <op declare match>) <character class> <adverb list discard>	name => 'discard rule/1'

# Expanded <single symbol> into its two possibilities, i.e. inlined
# its rules here, to make the differences visible to the semantics,
# i.e. actionable.

<lexeme rule>                  ::= (':lexeme' <op declare match>) symbol <adverb list lexeme>	name => 'lexeme rule/0'
<current lexer statement>      ::= ('current' 'lexer' 'is') <lexer name>			name => 'lexeme rule/1'

<completion event declaration> ::= ('event') <event initialization> ('=' 'completed') <symbol name>	name => 'completion event declaration/0'
<nulled event declaration>     ::= ('event') <event initialization> ('=' 'nulled')    <symbol name>	name => 'nulled event declaration/0'
<prediction event declaration> ::= ('event') <event initialization> ('=' 'predicted') <symbol name>	name => 'prediction event declaration/0'

<inaccessible statement> ::= ('inaccessible' 'is') <inaccessible treatment> ('by' 'default')		name => 'inaccessible statement/0'
<inaccessible treatment> ::=
   'warn'	name => 'inaccessible treatment/0'
 | 'ok'		name => 'inaccessible treatment/1'
 | 'fatal'	name => 'inaccessible treatment/2'

<priority rule> ::=
   lhs (<op declare bnf>  ) <priorities bnf>	name => 'priority rule/0'
 | lhs (<op declare match>) <priorities match>	name => 'priority rule/1'

<priorities bnf>   ::= <alternatives bnf>+	name => 'priorities bnf/0'
  separator => <op loosen>
  proper => 1

<priorities match> ::= <alternatives match>+	name => 'priorities match/0'
  separator => <op loosen>
  proper => 1

<alternatives bnf> ::= <alternative bnf>+	name => 'alternatives bnf/0'
  separator => <op equal priority>
  proper => 1

<alternatives match> ::= <alternative match>+	name => 'alternatives match/0'
  separator => <op equal priority>
  proper => 1

<alternative bnf>   ::= rhs <adverb list bnf alternative>	name => 'alternative bnf/0'
<alternative match> ::= rhs <adverb list match alternative>	name => 'alternative match/0'

<adverb list default>		::= <adverb list items default>			name => 'adverb list default/0'
<adverb list discard>		::= <adverb list items discard>			name => 'adverb list discard/0'
<adverb list lexeme>		::= <adverb list items lexeme>			name => 'adverb list lexeme/0'
<adverb list discard default>	::= <adverb list items discard default>		name => 'adverb list discard default/0'
<adverb list lexeme default>	::= <adverb list items lexeme default>		name => 'adverb list lexeme default/0'
<adverb list bnf alternative>	::= <adverb list items bnf alternative>		name => 'adverb list bnf alternative/0'
<adverb list bnf empty>		::= <adverb list items bnf empty>		name => 'adverb list bnf empty/0'
<adverb list bnf quantified>	::= <adverb list items bnf quantified>		name => 'adverb list bnf quantified/0'
<adverb list match alternative>	::= <adverb list items match alternative>	name => 'adverb list match alternative/0'
<adverb list match empty> 	::= <adverb list items match empty>		name => 'adverb list match empty/0'
<adverb list match quantified>	::= <adverb list items match quantified>	name => 'adverb list match quantified/0'

<adverb list items default>		::= <adverb item default>*		name => 'adverb list items default/0'		
<adverb list items discard>	    	::= <adverb item discard>*		name => 'adverb list items discard/0'		
<adverb list items lexeme>	    	::= <adverb item lexeme>*		name => 'adverb list items lexeme/0'		
<adverb list items discard default> 	::= <adverb item discard default>*	name => 'adverb list items discard default/0'	
<adverb list items lexeme default>  	::= <adverb item lexeme default>*	name => 'adverb list items lexeme default/0'	
<adverb list items bnf alternative> 	::= <adverb item bnf alternative>*	name => 'adverb list items bnf alternative/0'	
<adverb list items bnf empty>		::= <adverb item bnf empty>*		name => 'adverb list items bnf empty/0'	
<adverb list items bnf quantified>	::= <adverb item bnf quantified>*	name => 'adverb list items bnf quantified/0'	
<adverb list items match alternative>	::= <adverb item match alternative>*	name => 'adverb list items match alternative/0'
<adverb list items match empty>		::= <adverb item match empty>*		name => 'adverb list items match empty/0'	
<adverb list items match quantified>	::= <adverb item match quantified>*	name => 'adverb list items match quantified/0'

<adverb item default>		::=
   action			name => 'adverb item default/0'
 | <null adverb>		name => 'adverb item default/1'

<adverb item discard>	    	::=
   <event specification>	name => 'adverb item discard/0'
 | <null adverb>		name => 'adverb item discard/1'

<adverb item lexeme>	    	::=
   <event specification>	name => 'adverb item lexeme/0'
 | <latm specification>		name => 'adverb item lexeme/1'
 | <priority specification>	name => 'adverb item lexeme/2'
 | <pause specification>	name => 'adverb item lexeme/3'
 | <null adverb>		name => 'adverb item lexeme/4'

<adverb item discard default> 	::=
   <event specification>	name => 'adverb item discard default/0'
 | <null adverb>		name => 'adverb item discard default/1'

<adverb item lexeme default>  	::=
   action			name => 'adverb item lexeme default/0'
 | <latm specification>		name => 'adverb item lexeme default/1'
 | <null adverb>		name => 'adverb item lexeme default/2'

<adverb item bnf alternative> 	::=
   action			name => 'adverb item bnf alternative/0'
 | <left association>		name => 'adverb item bnf alternative/1'
 | <right association>		name => 'adverb item bnf alternative/2'
 | <group association>		name => 'adverb item bnf alternative/3'
 | naming 			name => 'adverb item bnf alternative/4'
 | <null adverb>		name => 'adverb item bnf alternative/5'

<adverb item bnf empty>		::=
    action			name => 'adverb item bnf empty/0'
 | <left association>		name => 'adverb item bnf empty/1'
 | <right association>		name => 'adverb item bnf empty/2'
 | <group association>		name => 'adverb item bnf empty/3'
 | naming			name => 'adverb item bnf empty/4'
 | <null adverb>		name => 'adverb item bnf empty/5'

<adverb item bnf quantified>	::=
   action			name => 'adverb item bnf quantified/0'
 | <separator specification>	name => 'adverb item bnf quantified/1'
 | <proper specification>	name => 'adverb item bnf quantified/2'
 | naming 			name => 'adverb item bnf quantified/3'
 | <null adverb>		name => 'adverb item bnf quantified/4'

<adverb item match alternative>	::=
   naming			name => 'adverb item match alternative/0'
 | <null adverb>		name => 'adverb item match alternative/1'

<adverb item match empty>	::=
   naming			name => 'adverb item match empty/0'
 | <null adverb>		name => 'adverb item match empty/1'

<adverb item match quantified>	::=
   <separator specification>	name => 'adverb item match quantified/0'
 | <proper specification>	name => 'adverb item match quantified/1'
 | <null adverb>		name => 'adverb item match quantified/2'

# NOTE: __unused__ <rank specification> <null ranking specification>

<null adverb>                ::= ','						name => 'null adverb/0'
action                       ::= ('action'       '=>') <action name>		name => 'action/0'
<left association>           ::= ('assoc'        '=>' 'left')			name => 'left association/0'
<right association>          ::= ('assoc'        '=>' 'right')			name => 'right association/0'
<group association>          ::= ('assoc'        '=>' 'group')			name => 'group association/0'
<separator specification>    ::= ('separator'    '=>') <single symbol>		name => 'separator specification/0'
<proper specification>       ::= ('proper'       '=>') boolean			name => 'proper specification/0'
<rank specification>         ::= ('rank'         '=>') <signed integer>		name => 'rank specification/0'
<priority specification>     ::= ('priority'     '=>') <signed integer>		name => 'priority specification/0'
<pause specification>        ::= ('pause'        '=>') <before or after>	name => 'pause specification/0'

<null ranking specification> ::=
   ('null-ranking' '=>') <null ranking constant>	name => 'null ranking specification/0'
 | ('null' 'rank'  '=>') <null ranking constant>	name => 'null ranking specification/1'

<latm specification>         ::=
   ('forgiving'    '=>') boolean	name => 'latm specification/0'
 | ('latm'         '=>') boolean	name => 'latm specification/1'

<null ranking constant>      ::=
   'low'	name => 'null ranking constant/0'
 | 'high'	name => 'null ranking constant/0'

<naming>                     ::= ('name' '=>') <alternative name>	name => 'naming/0'
<event specification>        ::= ('event' '=>') <event initialization>	name => 'event specification/0'

<event initialization>	     ::= <event name> <event initializer>	name => 'event initialization/0'

<event initializer> ::= ('=') <on or off>	name => 'event initializer/0'
<event initializer> ::=	      	     		name => 'event initializer/1'	# empty

<on or off> ::=
   'on'		name => 'on or off/0'
 | 'off'	name => 'on or off/1'

<alternative name> ::=
   <standard name>		name => 'alternative name/0'
 | <single quoted name>		name => 'alternative name/1'

<lexer name>       ::=
   <standard name>		name => 'lexer name/0'
 | <single quoted name>		name => 'lexer name/1'

<event name> ::=
   <standard name>		name => 'event name/0'
 | <single quoted name>		name => 'event name/1'
 | <reserved event name>	name => 'event name/2'

lhs ::= <symbol name>		name => 'lhs/0'
rhs ::= <rhs primary>+		name => 'rhs/0'

<rhs primary> ::=
   <single symbol>			name => 'rhs primary/0'
 | <single quoted string>		name => 'rhs primary/1'
 | <parenthesized rhs primary list>	name => 'rhs primary/2'

<parenthesized rhs primary list> ::= ('(') <rhs primary list> (')')
	name => 'parenthesized rhs primary list/0'

<rhs primary list> ::= <rhs primary>+	name => 'rhs primary list/0'

<single symbol> ::=
    symbol		name => 'single symbol/0'
  | <character class>	name => 'single symbol/1'

symbol ::= <symbol name>	name => 'symbol/0'

<symbol name> ::=
   <bare name>		name => 'symbol name/0'
 | <bracketed name>	name => 'symbol name/1'

<action name> ::=
   <Perl name>			name => 'action name/0'
 | <reserved action name>	name => 'action name/1'
 | <array descriptor>		name => 'action name/2'

quantifier ::=
   '*'		name => 'quantifier/0'
 | '+'		name => 'quantifier/1'

# # ## ### ##### ######## ############# #####################
## Lexing, whitespace, including comments.

:discard ~ whitespace
whitespace ~ [[:space:]]+

# allow comments
:discard ~ <hash comment>

<hash comment> ~
   <terminated hash comment>
 | <unterminated final hash comment>

<terminated hash comment>         ~ '#' <hash comment body> <vertical space char>
<unterminated final hash comment> ~ '#' <hash comment body>

<hash comment body> ~ <hash comment char>*

<vertical space char> ~  [\n\v\f\r\u2028\u2029]
<hash comment char>   ~ [^\n\v\f\r\u2028\u2029]

# # ## ### ##### ######## ############# #####################

<op declare bnf> ~ '::='
<op declare match> ~ '~'
<op loosen> ~ '||'
<op equal priority> ~ '|'
<before or after> ~ 'before' | 'after'

<signed integer> ~ <integer> | <sign> <integer>
<sign> ~ [+\55]
<integer> ~ [[:digit:]]+

boolean ~ [01]

<reserved event name>          ~ <double colon> <one or more word characters>
<reserved action name>         ~ <double colon> <one or more word characters>
<one or more word characters>  ~ [[:alnum:]_]+
<zero or more word characters> ~ [[:alnum:]_]*

# Perl identifiers allow an initial digit, which makes them slightly
# more liberal than Perl bare names but equivalent to Perl names with
# sigils.

<Perl identifier> ~ <one or more word characters>
<double colon> ~ '::'
<Perl name> ~ <Perl identifier>+
  separator => <double colon>
  proper    => 1

<bare name>      ~ <one or more word characters>
<standard name>  ~ [a-zA-Z] <zero or more word characters>
<bracketed name> ~ '<' <bracketed name string> '>'
<bracketed name string> ~ [[:alnum:]_[:space:]]+

<array descriptor> ~ <array descriptor left bracket>
                       <result item descriptor list>
		     <array descriptor right bracket>

<array descriptor left bracket> ~ '['
<array descriptor left bracket> ~ '[' whitespace

<array descriptor right bracket> ~ ']'
<array descriptor right bracket> ~ whitespace ']'

<result item descriptor list> ~ <result item descriptor>*
  separator => <result item descriptor separator>

<result item descriptor separator> ~ ','
<result item descriptor separator> ~ ',' whitespace

<result item descriptor> ~
    'start'
  | 'length'
  | 'g1start'
  | 'g1length'
  | 'name'
  | 'lhs'
  | 'symbol'
  | 'rule'
  | 'value'
  | 'values'
  | 'ord'

# # ## ### ##### ######## ############# #####################
## Strings of various kinds.

# For an explanation please look at the beginning of the file.
# The escapes handled here are (as a list of representative examples):
#
## \a   \r      \0      \uF    \uFFFFF
## \b   \t      \10     \uFF   \uFFFFFF
## \f   \v      \100    \uFFF
## \n   \\      \xFF    \uFFFF \UFFFFFFFF
#
## In strings single-quotes (['], \134) may not appear directly.  In
## char-classes dash ('-', \55) and closing bracket (']', \135) may
## not appear directly. The line and paragraph separator characters
## (\u2028, \u2029) are forbidden as well.

<single quoted string> ~ <a single quoted string> <character class modifiers>
<single quoted name>   ~ <a single quoted string>

<a single quoted string> ~ <string open> <non empty string>

<non empty string>
        ~ <plain string char> <nullable string>
        | <escaped string char>

<escaped string char> ~ escape <escaped string char details>

<nullable string>
        ~ <string close>
        | <non empty string>

<escaped string char details>
        ~ control                             <nullable string>
        | 'x' hex hex                         <nullable string>
        | 'U' hex hex hex hex hex hex hex hex <nullable string>
        | 'u' hex hex hex hex hex hex         <nullable string>
        | 'u' hex hex hex hex hex             <nullable string post short unicode escape>
        | 'u' hex hex hex hex                 <nullable string post short unicode escape>
        | 'u' hex hex hex                     <nullable string post short unicode escape>
        | 'u' hex hex                         <nullable string post short unicode escape>
        | 'u' hex                             <nullable string post short unicode escape>
        | <leading octal> octal octal         <nullable string>
        | octal octal                         <nullable string post short octal escape>
        | octal                               <nullable string post short octal escape>

<nullable string post short unicode escape>
        ~ <escaped string char>
        | <plain string char without hex> <nullable string>
        | <string close>

<nullable string post short octal escape>
        ~ <escaped string char>
        | <plain string char without octal> <nullable string>
        | <string close>

<string open>   ~ [']
<string close>  ~ [']

octal           ~ [0-7]
<leading octal> ~ [0-3]
hex             ~ [[:xdigit:]]
control         ~ [abfnrtv\\]
escape          ~ '\\'

<plain string char>               ~ [^\a\b\f\n\r\v\\\u2028\u2029']
<plain string char without hex>   ~ [^[:xdigit:]\a\b\f\n\r\v\\\u2028\u2029']
<plain string char without octal> ~ [^0-7\a\b\f\n\r\v\\\u2028\u2029']

# # ## ### ##### ######## ############# #####################
## Character classes

# They allow the sames escapes as strings, so we have to take care of
# preventing ambiguity here as well. The same trick is used. However
# the individual elements have different Follow() contexts and a
# different end marker. This requires us to create all new symbols.

# The low-level octal and hexadecimal digits, the special characters,
# etc. can be re-used however, see the string section above for their
# definitions.

<character class> ~ <a character class> <character class modifiers>

<a character class> ~ <cc open> <cc element and more>

<cc element and more>
        ~ <plain cc element>
        | <escaped cc element>

<plain cc element>
	~ <plain cc char> <cc range completion>
	| <posix char class> <nullable cc>
	
<plain cc element without hex>
	~ <plain cc char without hex> <cc range completion>
	| <posix char class> <nullable cc>

<plain cc element without octal>
	~ <plain cc char without octal> <cc range completion>
	| <posix char class> <nullable cc>

<cc range completion>
        ~ '-' <plain cc char> <nullable cc>
        | '-' <escaped cc char>
        | <nullable cc>

<nullable cc>
        ~ <cc close>
        | <cc element and more>

<escaped cc element> ~ escape <escaped cc element details>

<escaped cc char> ~ escape <escaped cc char details>

<escaped cc element details>
        ~ control                             <cc range completion>
        | 'x' hex hex                         <cc range completion>
        | 'U' hex hex hex hex hex hex hex hex <cc range completion>
        | 'u' hex hex hex hex hex hex         <cc range completion>
        | 'u' hex hex hex hex hex             <cc range completion post short unicode escape>
        | 'u' hex hex hex hex                 <cc range completion post short unicode escape>
        | 'u' hex hex hex                     <cc range completion post short unicode escape>
        | 'u' hex hex                         <cc range completion post short unicode escape>
        | 'u' hex                             <cc range completion post short unicode escape>
        | <leading octal> octal octal         <cc range completion>
        | octal octal                         <cc range completion post short octal escape>
        | octal                               <cc range completion post short octal escape>

<cc range completion post short unicode escape>
        ~ '-' <plain cc char> <nullable cc>
        | '-' <escaped cc char>
        | <escaped cc element>
        | <plain cc element without hex>
        | <cc close>

<cc range completion post short octal escape>
        ~ '-' <plain cc char> <nullable cc>
        | '-' <escaped cc char>
        | <escaped cc element>
        | <plain cc element without octal>
        | <cc close>

<escaped cc char details>
        ~ control                     <nullable cc>
        | 'x' hex hex                 <nullable cc>
        | 'u' hex hex hex hex         <nullable cc>
        | 'u' hex hex hex             <nullable cc post short unicode escape>
        | 'u' hex hex                 <nullable cc post short unicode escape>
        | 'u' hex                     <nullable cc post short unicode escape>
        | <leading octal> octal octal <nullable cc>
        | octal octal                 <nullable cc post short octal escape>
        | octal                       <nullable cc post short octal escape>

<nullable cc post short unicode escape>
        ~ <escaped cc char>
        | <plain cc char without hex> <nullable cc>
        | <cc close>

<nullable cc post short octal escape>
        ~ <escaped cc char>
        | <plain cc char without octal> <nullable cc>
        | <cc close>

<cc close> ~ ']'
<cc open>
        ~ '['
        | '[^'

<plain cc char>               ~ [^\a\b\f\n\r\t\v\\\u2028\u2029\135\55]
<plain cc char without hex>   ~ [^[:xdigit:]\a\b\f\n\r\t\v\\\u2028\u2029\135\55]
<plain cc char without octal> ~ [^0-7\a\b\f\n\r\t\v\\\u2028\u2029\135\55]

<posix char class>      ~ '[:' <posix char class name> ':]'
<posix char class name> ~ [[:alnum:]]+

<character class modifiers> ~ <character class modifier>*
<character class modifier> ~ ':ic'
<character class modifier> ~ ':i'
