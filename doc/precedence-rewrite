
In what follows, `exp` will represent the LHS symbol of the
precedenced rule.  We will need to have a unique symbol for `exp` at
each level of precedence.  We will represent the symbol for `exp` at
level `x` as `exp[x]`.

The top level rule for the precedenced rule will be a rule whose LHS
is the precedenced rule's LHS, and whose RHS is the symbol for `exp`
at the the __lowest__ precedence.  That is, the top level rule will be

```
exp ::= exp[0]
```

### The "spine"

In many expressions, there is no logic at a given precedence level, so
that we need to simply "fall through" to the next-tighter level of
precedence.  Therefore, for every precedence `x`, such that `x` is
less than `tightest`, we add the rule

```
   exp[x] ::= exp[tighter(x)]
```

For example, if the precedence ran from `loosest == 0` to `tightest ==
4`, we would add the BNF rules

```
    exp[0] ::= exp[1]
    exp[1] ::= exp[2]
    exp[2] ::= exp[3]
    exp[3] ::= exp[4]
```

### Left association

We now can deal with the RHS alternatives in the work list.

Let `curr` be the precedence of the RHS alternative.

If a RHS alternative has left association (the default) we rewrite the
RHS, replacing all occurrences of `exp`.

We replace the leftmost occurrence of `exp` with `exp[curr]`, and the
others with `exp[tighter(curr)]`.  We then add a rule with the
rewritten RHS, and `exp[curr]` as the LHS.

For example, if the RHS alternative is
```
    exp + exp
```
we add the BNF rule
```
    exp[curr] ::= exp[curr] + exp[tighter(curr)]
```

Note that `exp` may not occur on the RHS, in which case no RHS
replacements will be necessary.

### Right association

Right association is handled in a way that is symmetric with left
association.

Again, let `curr` be the precedence of the RHS alternative.

If a RHS alternative has right association, we rewrite the RHS,
replacing all occurrences of `exp`.

We replace the *rightmost* occurrence of `exp` with `exp[curr]`, and
the others with `exp[tighter(curr)]`.  We then add a rule with the
rewritten RHS, and `exp[curr]` as the LHS.

For example, if the RHS alternative is
```
    exp ** exp
```
we add the BNF rule
```
    exp[curr] ::= exp[tighter(curr)] ** exp[curr]
```

### Group association

The archetypal case for group association is the parenthesis operator.

Intuitively, group association allows any expression to occur inside
another "surrounding" expression.  In the case of the parentheses, of
course, they actually surround their contents lexically.

Let `curr` be the precedence of the RHS alternative.

If a RHS alternative has group association, we rewrite the RHS,
replacing all occurrences of `exp` with `exp[0]`, We then add a rule
with the rewritten RHS, and `exp[curr]` as the LHS.

For example, if the RHS alternative is
```
    ( exp )
```
we add the BNF rule
```
    exp[curr] ::= ( exp[0] )
```
